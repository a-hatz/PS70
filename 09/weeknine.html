<!DOCTYPE html>
<html lang="en">

<title>PS70: Intro to Digital Fabrication </title>
<link href="https://cdn.jsdelivr.net/npm/bootstrap@5.1.1/dist/css/bootstrap.min.css" rel="stylesheet">
  <link href="../style.css" rel="stylesheet">

<nav class="navbar navbar-expand-sm navbar-light bg-light">
  <div style="align-items: center; justify-content:center;" class="container-fluid">
    <h3 class="nav-title"> PS70: Intro to Digital Fabrication</h3>
    <div class="navbar-nav">
      <h4><a class="nav-link" href="../index.html">Home</a></h4>
      <h4><a class="nav-link" href="../about.html">About</a></h4>
      <h4><a class="nav-link" href="../journal.html">Journal</a></h4>
    </div>
  </div>
</nav>

<body>



<xmp style="display:none;">

### Communications

## Concept & Inspirations

I found the idea of an ESP32 wed server very interesting, but instead of turning lights on and off I thought it might be cool to control words on an OLED screen. 

## Design Process

I largely based my code on this RandomNerd tutorial ([tutorial](https://randomnerdtutorials.com/esp32-web-server-arduino-ide/)). The idea was that the IP address would take you to a website where you could enter words to be printed on an OLED screen connectdd to the ESP32.

<pre><code>
  #include <WiFi.h>
  #include <U8g2lib.h>

  U8G2_SH1106_128X64_NONAME_F_HW_I2C u8g2(U8G2_R0, /* reset=*/ U8X8_PIN_NONE);

  // Replace with your network credentials
  const char* ssid = "*";
  const char* password = "*";

  WiFiServer server(80);

  String header;

  void setup() {
    Serial.begin(115200);

    u8g2.begin();
    u8g2.setFont(u8g2_font_ncenB14_tr); // Set font for the display

    Serial.print("Connecting to ");
    Serial.println(ssid);
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
      delay(500);
      Serial.print(".");
    }
    Serial.println("");
    Serial.println("WiFi connected.");
    Serial.println("IP address: ");
    Serial.println(WiFi.localIP());
    server.begin();
  }

  void loop() {
    WiFiClient client = server.available();

    if (client) {
      Serial.println("New Client.");
      String currentLine = "";
      while (client.connected()) {
        if (client.available()) {
          char c = client.read();
          Serial.write(c);
          header += c;
          if (c == '\n') {
            if (currentLine.length() == 0) {
              client.println("HTTP/1.1 200 OK");
              client.println("Content-type:text/html");
              client.println("Connection: close");
              client.println();

              if (header.indexOf("GET /text1") >= 0) {
                Serial.println("Displaying Text 1");
                displayText("Hello, World!"); // Display Text 1 on OLED
              } else if (header.indexOf("GET /text2") >= 0) {
                Serial.println("Displaying Text 2");
                displayText("Welcome to ESP32!"); // Display Text 2 on OLED
              }

              client.println("<!DOCTYPE html><html>");
              client.println("<head><meta name=\"viewport\" content=\"width=device-width, initial-scale=1\">");
              client.println("<link rel=\"icon\" href=\"data:,\">");
              client.println("<style>html { font-family: Helvetica; display: inline-block; margin: 0px auto; text-align: center;}");
              client.println(".button { background-color: #4CAF50; border: none; color: white; padding: 16px 40px;");
              client.println("text-decoration: none; font-size: 30px; margin: 2px; cursor: pointer;}");
              client.println(".button2 {background-color: #555555;}</style></head>");

              client.println("<body><h1>ESP32 Web Server</h1>");
              client.println("<p>Choose Text:</p>");
              client.println("<p><a href=\"/text1\"><button class=\"button\">Text 1</button></a></p>");
              client.println("<p><a href=\"/text2\"><button class=\"button\">Text 2</button></a></p>");
              client.println("</body></html>");

              client.println();
              break;
            } else {
              currentLine = "";
            }
          } else if (c != '\r') {
            currentLine += c;
          }
        }
      }
      header = "";
      client.stop();
      Serial.println("Client disconnected.");
      Serial.println("");
    }
  }

  void displayText(const char* text) {
    u8g2.clearBuffer(); // Clear the display buffer
    u8g2.setCursor(0, 20); // Set the cursor position (x, y)
    u8g2.setFont(u8g2_font_ncenB14_tr); // Set the font style
    u8g2.print(text); // Print the text on the display
    u8g2.sendBuffer(); // Send the buffer to the display
  }


</code></pre> 

</xmp>
</body>

<script src="../strapdown.js"></script>
<script src="https://cdn.jsdelivr.net/npm/bootstrap@5.0.2/dist/js/bootstrap.bundle.min.js" ></script>

</html>